definition {

	macro addComment {
		Click(locator1 = "BridgeDemos#SHOW_HIDE_COMMENTS_LINK");

		Type(
			key_text = "comments",
			locator1 = "TextArea#ANY",
			value1 = "${comments}");

		Click(locator1 = "BridgeDemos#SUBMIT_BUTTON");
	}

	macro autoPopulateCityState {
		Type(
			key_text = "Zip/Postal",
			locator1 = "TextInput#ANY",
			value1 = "${postalCode}");

		Click(
			key_text = "City",
			locator1 = "TextInput#ANY");

		AssertTextEquals(
			key_text = "City",
			locator1 = "TextInput#ANY",
			value1 = "${city}");

		AssertTextEquals(
			key_text = "State/Province",
			locator1 = "BridgeDemos#STATE_PROVINCE_SELECTED",
			value1 = "${state}");
	}

	macro editDateFormat {
		BridgeDemos.gotoEdit();

		Type(
			locator1 = "BridgeDemos#DATE_PATTERN",
			value1 = "${dateFormat}");

		Click(locator1 = "BridgeDemos#SUBMIT_BUTTON");

		BridgeDemos.viewDateFormat(
			dateFormat = "${dateFormat}",
			displayDate = "${displayDate}");
	}

	macro gotoEdit {
		AssertClick(
			locator1 = "BridgeDemos#EDIT_LINK",
			value1 = "Edit");
	}

	macro submit {
		Type(
			key_text = "First Name",
			locator1 = "TextInput#ANY",
			value1 = "${firstName}");
		Type(
			key_text = "Last Name",
			locator1 = "TextInput#ANY",
			value1 = "${lastName}");
		Type(
			key_text = "Email Address",
			locator1 = "TextInput#ANY",
			value1 = "${emailAddress}");
		Type(
			key_text = "Phone Number",
			locator1 = "TextInput#ANY",
			value1 = "${phoneNumber}");

		if (!("${applicantName}" == "JSF JSP")) {
			BridgeDemos.autoPopulateCityState(
				city = "${city}",
				postalCode = "${postalCode}",
				state = "${state}");

			BridgeDemos.addComment(comments = "${comments}");

			AssertTextEquals.assertPartialText(
				locator1 = "BridgeDemos#CONFIRMATION_FORM",
				value1 = "${confirmation}");
		}

		else {
			Type(
				key_text = "City",
				locator1 = "TextInput#ANY",
				value1 = "${city}");

			Select(
				locator1 = "BridgeDemos#STATE_PROVINCE",
				value1 = "${state}");
			Type(
				key_text = "Zip/Postal",
				locator1 = "TextInput#ANY",
				value1 = "${postalCode}");
		}
	}

	macro uploadFile {
		if ("${applicantName}" == "JSF JSP") {
			Click(locator1 = "BridgeDemos#ADD_ATTACHMENT");
		}

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${fileName}");

		if ("${applicantName}" == "Prime Faces" || "${applicantName}" == "Rich JSP") {
			Click(locator1 = "BridgeDemos#UPLOAD_BUTTON");
		}

		else {
			Click(locator1 = "BridgeDemos#SUBMIT_FILE_BUTTON");
		}

		AssertTextEquals(
			locator1 = "BridgeDemos#FILE_NAME_TABLE",
			value1 = "${fileName}");
	}

	macro validateField {
		Navigator.openWithAppendToBaseURL(urlAppend = "${urlAppend}");

		if ("${applicantName}" == "JSF JSP") {
			BridgeDemos.submit(
				applicantName = "JSF JSP",
				city = "Orlando",
				confirmation = "Thank you David",
				emailAddress = "no_need@just.pray",
				firstName = "David",
				lastName = "Samuel",
				phoneNumber = "1112223333",
				postalCode = "32801",
				state = "FL");
		}

		Type(
			key_text = "${fieldName}",
			locator1 = "TextInput#ANY",
			value1 = "${fieldValue}");

		Click(
			key_text = "City",
			locator1 = "TextInput#ANY");

		if ("${applicantName}" == "Butter Faces") {
			AssertElementPresent(
				key_fieldName = "${fieldName}",
				locator1 = "BridgeDemos#FIELD_ERROR_BUTTER_FACES_TOAST");
		}

		else if ("${applicantName}" == "JSF JSP") {
			Click(locator1 = "BridgeDemos#SUBMIT_BUTTON");

			AssertTextEquals.assertPartialText(
				key_fieldName = "${fieldName}",
				locator1 = "BridgeDemos#FIELD_ERROR_JSF_JSP",
				value1 = "${errorMessage}");
		}

		else {
			BridgeDemos.viewFieldErrorMessage(
				applicantName = "${applicantName}",
				errorMessage = "${errorMessage}",
				fieldName = "${fieldNameText}");
		}
	}

	macro viewApplication {
		for (var key_text : list "First Name,Last Name,Email Address,Phone Number,Birthday,City,Zip/Postal") {
			AssertElementPresent(
				key_text = "${key_text}",
				locator1 = "TextInput#ANY");
		}

		AssertElementPresent(
			key_text = "State/Province",
			locator1 = "BridgeDemos#STATE_PROVINCE");

		if (!("${applicantName}" == "JSF JSP")) {
			AssertElementPresent(locator1 = "BridgeDemos#SHOW_HIDE_COMMENTS_LINK");
		}

		for (var key_libraryName : list "Mojarra,Liferay Faces Alloy,Liferay Faces Bridge Impl,Liferay Faces Bridge Ext") {
			AssertElementPresent(
				key_text = "${key_text}",
				locator1 = "TextInput#ANY");
		}
	}

	macro viewComment {
		if (IsElementNotPresent(locator1 = "TextArea#ANY", key_tab = "comments")) {
			Click(locator1 = "BridgeDemos#SHOW_HIDE_COMMENTS_LINK");
		}

		AssertTextEquals(
			key_text = "comments",
			locator1 = "TextArea#ANY",
			value1 = "${comments}");
	}

	macro viewDateFormat {
		var displayDate = DateUtil.getFormattedCurrentDate("${dateFormat}");

		AssertTextEquals(
			key_text = "Birthday",
			locator1 = "TextInput#ANY",
			value1 = "${displayDate}");
	}

	macro viewFieldErrorMessage {
		if ("${applicantName}" == "Butter Faces") {
			AssertTextEquals.assertPartialText(
				key_fieldName = "${fieldName}",
				locator1 = "BridgeDemos#FIELD_ERROR_BUTTER_FACES",
				value1 = "${errorMessage}");
		}

		else if ("${applicantName}" == "JSF JSP") {
			AssertTextEquals.assertPartialText(
				key_fieldName = "${fieldName}",
				locator1 = "BridgeDemos#FIELD_ERROR_JSF_JSP",
				value1 = "${errorMessage}");
		}
		else {
			AssertTextEquals.assertPartialText(
				key_fieldName = "${fieldName}",
				locator1 = "BridgeDemos#FIELD_ERROR",
				value1 = "${errorMessage}");
		}
	}

}