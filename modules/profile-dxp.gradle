import com.liferay.gradle.util.GradleUtil
import com.liferay.gradle.util.Validator

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

task checkoutDXPScript
task removeDXPScript

File repositoryDir = new File(projectDir.parentFile, "git-clone-profile-dxp")
String repositoryURL = "git@github.com:liferay/liferay-portal-ee.git"

if (System.getenv("JENKINS_HOME")) {
	repositoryURL = "git@github-dev.liferay.com:liferay/liferay-portal-ee.git"
}

checkoutDXPScript {
	doLast {
		String branchName = GradleUtil.getProperty(project, "git.working.branch.name", (String)null)

		if (!branchName.endsWith("-private")) {
			branchName = branchName + "-private"
		}

		logger.lifecycle "git clone --branch={} --depth=1 --no-checkout --no-tags --single-branch {} {}", branchName, repositoryURL, repositoryDir.name

		exec {
			commandLine "git", "clone", "--branch=" + branchName, "--depth=1", "--no-checkout", "--no-tags", "--single-branch", repositoryURL, repositoryDir.name
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "checkout", "refs/heads/" + branchName, "--", "build-working-dir.xml", "working.dir.properties"
			workingDir = repositoryDir
		}
	}
}

removeDXPScript {
	doLast {
		delete repositoryDir
	}
}