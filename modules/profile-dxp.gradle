import com.liferay.gradle.util.GradleUtil
import com.liferay.gradle.util.Validator

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript
}

task checkoutDXPScript
task removeDXPScript

File gitRepositoryDir = new File(projectDir.parentFile, "git-clone-profile-dxp")
String gitRepositoryURL = "git@github.com:liferay/liferay-portal-ee.git"

if (System.getenv("JENKINS_HOME")) {
	gitRepositoryURL = "git@github-dev.liferay.com:liferay/liferay-portal-ee.git"
}

checkoutDXPScript {
	doLast {
		String gitBranchName = GradleUtil.getProperty(project, "git.working.branch.name", (String)null)

		if (!gitBranchName.endsWith("-private")) {
			gitBranchName = gitBranchName + "-private"
		}

		logger.lifecycle "git clone --branch={} --depth=1 --no-checkout --no-tags --single-branch {} {}", gitBranchName, gitRepositoryURL, gitRepositoryDir.name

		exec {
			commandLine "git", "clone", "--branch=" + gitBranchName, "--depth=1", "--no-checkout", "--no-tags", "--single-branch", gitRepositoryURL, gitRepositoryDir.name
			workingDir = projectDir.parentFile
		}

		exec {
			commandLine "git", "checkout", "refs/heads/" + gitBranchName, "--", "build-working-dir.xml", "working.dir.properties"
			workingDir = gitRepositoryDir
		}
	}
}

removeDXPScript {
	doLast {
		delete gitRepositoryDir
	}
}